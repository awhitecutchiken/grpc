plugins {
    id "java-library"
    id "maven-publish"

    id "me.champeau.gradle.japicmp"
}

description = "gRPC: Testing"

evaluationDependsOn(project(':grpc-core').path)

dependencies {
    api project(':grpc-api'),
            project(':grpc-core'),
            project(':grpc-context'),
            project(':grpc-stub'),
            libraries.jsr305,
            libraries.junit
    compileOnly libraries.guava_failureaccess,
            libraries.j2objc_annotations
    guavaDependency 'implementation'
    censusApiDependency 'implementation'

    testImplementation (libraries.mockito) {
        // prefer our own versions instead of mockito's dependency
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }

    testImplementation project(':grpc-testing-proto'),
            project(':grpc-core').sourceSets.test.output
}

javadoc { exclude 'io/grpc/internal/**' }

jacocoTestReport {
    classDirectories.from = sourceSets.main.output.collect {
        fileTree(dir: it,
        exclude: [
                '**/io/grpc/internal/testing/**',
        ])
    }
}
