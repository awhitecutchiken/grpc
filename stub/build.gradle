plugins {
    id "java-library"
    id "maven-publish"

    id "me.champeau.gradle.japicmp"
    id "ru.vyarus.animalsniffer"
}

description = "gRPC: Stub"
dependencies {
    api project(':grpc-api'),
            libraries.errorprone
    api (libraries.guava) { // Generated stubs need guava to compile.
        // prefer our own version from grpc-api instead of Guava's dependency
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        // prefer our own version from libraries instead of Guava's dependency
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        // prefer our own version from libraries instead of Guava's dependency
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }
    runtimeOnly libraries.animalsniffer_annotations
    testImplementation project(':grpc-testing'),
            libraries.truth
            
    signature "org.codehaus.mojo.signature:java17:1.0@signature"
    signature "net.sf.androidscents.signature:android-api-level-14:4.0_r4@signature"
}

javadoc.options.links "https://google.github.io/guava/releases/${guavaVersion}/api/docs/"
