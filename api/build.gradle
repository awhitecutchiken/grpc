plugins {
    id "java-library"
    id "java-test-fixtures"
    id "maven-publish"

    id "me.champeau.jmh"
    id "ru.vyarus.animalsniffer"
}

description = 'gRPC: API'

sourceSets {
    context {
        java {
            srcDir "${projectDir}/context/src/main/java"
        }
    }
    main {
        output.classesDirs.from(sourceSets.context.output.classesDirs)
    }
    test {
        java {
            srcDir "${projectDir}/context/src/test/java"
        }
    }
}

compileContextJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}


dependencies {
    compileOnly sourceSets.context.output
    testFixturesApi libraries.truth
    // Explicitly choose the guava version to stay Java 7-compatible.
    testFixturesImplementation 'com.google.guava:guava:30.1.1-android'
    testFixturesImplementation libraries.jsr305
    api libraries.jsr305,
            libraries.errorprone.annotations
    implementation libraries.guava

    testFixturesImplementation libraries.guava,
            libraries.junit,
            libraries.mockito.core
    testImplementation project(':grpc-testing'),
            project(':grpc-grpclb')
    testImplementation libraries.guava.testlib
    jmh project(':grpc-core')

    signature libraries.signature.java
    signature libraries.signature.android
}

tasks.named("javadoc").configure {
    // We want io.grpc.Internal, but not io.grpc.Internal*
    exclude 'io/grpc/Internal?*.java'
}

components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) { skip() }
components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) { skip() }
