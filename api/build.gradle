plugins {
    id "java-library"
    id "maven-publish"

    id "me.champeau.gradle.jmh"
    id "ru.vyarus.animalsniffer"
}

description = 'gRPC: API'

evaluationDependsOn(project(':grpc-context').path)

dependencies {
    api project(':grpc-context'),
            libraries.jsr305
    implementation libraries.errorprone
    implementation (libraries.guava) {
        // prefer our own versions from libraries instead of Guava's dependency
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }
    runtimeOnly libraries.animalsniffer_annotations
    testImplementation project(':grpc-context').sourceSets.test.output,
            project(':grpc-grpclb'),
            project(':grpc-testing'),
            libraries.guava_testlib

    jmh project(':grpc-core')

    signature "org.codehaus.mojo.signature:java17:1.0@signature"
    signature "net.sf.androidscents.signature:android-api-level-14:4.0_r4@signature"
}

javadoc {
    // We want io.grpc.Internal, but not io.grpc.Internal*
    exclude 'io/grpc/Internal?*.java'
}
