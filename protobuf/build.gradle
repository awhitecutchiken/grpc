plugins {
    id "java-library"
    id "maven-publish"

    id "com.google.protobuf"
    id "me.champeau.gradle.japicmp"
    id "ru.vyarus.animalsniffer"
}

description = 'gRPC: Protobuf'

dependencies {
    api project(':grpc-api'),
            libraries.jsr305,
            libraries.protobuf
    implementation libraries.guava

    api (libraries.google_api_protos) {
        // 'com.google.api:api-common' transitively depends on auto-value, which breaks our
        // annotations.
        exclude group: 'com.google.api', module: 'api-common'
        // Prefer our more up-to-date protobuf over 3.2.0
        // TODO: this is excluding a newer version of protobuf, which is a
        // no-no, but we need to upgrade protobuf separately before removing this.
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }

    api (project(':grpc-protobuf-lite')) {
        exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
    }

    signature "org.codehaus.mojo.signature:java17:1.0@signature"
}

javadoc.options.links 'https://developers.google.com/protocol-buffers/docs/reference/java/'
