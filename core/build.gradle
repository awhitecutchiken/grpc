// Add dependency on the protobuf plugin
buildscript {
    repositories {
        maven {
            // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
        }
    }
    dependencies {
        classpath libraries.protobuf_plugin
    }
}

description = 'gRPC: Core'

dependencies {
    compile project(':grpc-context'),
            libraries.gson,
            libraries.guava,
            libraries.errorprone,
            libraries.jsr305
    compile (libraries.opencensus_api) {
        // prefer 3.0.0 from libraries instead of 3.0.1
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        // prefer 2.1.2 from libraries instead of 2.2.0
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        // prefer 20.0 from libraries instead of 19.0
        exclude group: 'com.google.guava', module: 'guava'
        // we'll always be more up-to-date
        exclude group: 'io.grpc', module: 'grpc-context'
    }
    compile (libraries.opencensus_contrib_grpc_metrics) {
        // prefer 3.0.0 from libraries instead of 3.0.1
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        // we'll always be more up-to-date
        exclude group: 'io.grpc', module: 'grpc-context'
        // prefer 2.1.2 from libraries instead of 2.2.0
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        // prefer 20.0 from libraries instead of 19.0
        exclude group: 'com.google.guava', module: 'guava'
    }

    testCompile project(':grpc-context').sourceSets.test.output,
                project(':grpc-testing'),
                libraries.google_api_protos,
                libraries.protobuf

    signature "org.codehaus.mojo.signature:java16:1.1@signature"
}

javadoc {
    // We want io.grpc.Internal, but not io.grpc.Internal*
    exclude 'io/grpc/Internal?*.java'
    exclude 'io/grpc/internal/**'
}

animalsniffer {
    // Don't check sourceSets.jmh
    sourceSets = [sourceSets.main, sourceSets.test]
}

compileTestJava {
    options.compilerArgs += "-Xlint:-deprecation" // for enums in proto
}

configureProtoCompilation()
