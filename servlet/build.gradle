plugins {
    id "java-library"
    id "maven-publish"
}

description = "gRPC: Servlet"
sourceCompatibility = 1.8
targetCompatibility = 1.8

def jetty9Version = '9.4.43.v20210629'
def jetty10Version = '10.0.6'
def jetty11Version = '11.0.6';//TODO offer a grpc-servlet-jakarta build

configurations {
    undertowTestCompile.extendsFrom(testImplementation)
    tomcatTestCompile.extendsFrom(testImplementation)
    jetty9TestCompile.extendsFrom(testImplementation)
    jetty10TestCompile.extendsFrom(testImplementation)
    jetty11TestCompile.extendsFrom(testImplementation)
}

sourceSets {
    // Disable plain test, we only want to run tests per runtime
    test {
        java {
            exclude '**/*.java'
        }
    }
    // Create a test sourceset for each classpath - could be simplified if we made new test directories
    undertowTest {
        java {
            srcDir 'src/test/java'
            include '**/Undertow*.java'
        }
        compileClasspath = sourceSets.main.output + configurations.undertowTestCompile
        runtimeClasspath = sourceSets.main.output + configurations.testRuntimeClasspath + configurations.undertowTestCompile + sourceSets.undertowTest.output
    }
    tomcatTest {
        java {
            srcDir 'src/test/java'
            include '**/Tomcat*.java'
        }
        compileClasspath = sourceSets.main.output + configurations.tomcatTestCompile
        runtimeClasspath = sourceSets.main.output + configurations.testRuntimeClasspath + configurations.tomcatTestCompile + sourceSets.tomcatTest.output
    }
    jetty9Test {
        java {
            srcDir 'src/test/java'
            include '**/Jetty*.java'
            compileClasspath = sourceSets.main.output + configurations.jetty9TestCompile
            runtimeClasspath = sourceSets.main.output + configurations.testRuntimeClasspath + configurations.jetty9TestCompile + sourceSets.jetty9Test.output
        }
    }

    if (JavaVersion.current().isJava11Compatible()) {
        jetty10Test {
            java {
                srcDir 'src/test/java'
                include '**/Jetty*.java'
            }
            compileClasspath = sourceSets.main.output + configurations.jetty10TestCompile
            runtimeClasspath = sourceSets.main.output + configurations.testRuntimeClasspath + configurations.jetty10TestCompile + sourceSets.jetty10Test.output
        }
//        jetty11Test {
//            java {
//                srcDir 'src/test/java'
//                include '**/Jetty*.java'
//            }
//            compileClasspath = sourceSets.main.output + configurations.jetty11TestCompile
//            runtimeClasspath = sourceSets.main.output + configurations.testRuntimeClasspath + configurations.jetty11TestCompile + sourceSets.jetty11Test.output
//        }
    }
}

dependencies {
    api project(':grpc-core')
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1',
            libraries.javax_annotation // java 9, 10 needs it

    // Jetty9 workaround, we won't include this as a transitive dependency or otherwise
    // use at runtime, unless it is already present
    compileOnly "org.eclipse.jetty.http2:http2-server:${jetty9Version}"

    implementation libraries.guava

    testImplementation project(':grpc-stub'),
            project(':grpc-protobuf'),
            project(':grpc-servlet'),
            project(':grpc-netty'),
            project(':grpc-testing'),
            project(':grpc-auth'),
            project(':grpc-core').sourceSets.test.output,
            project(':grpc-netty').sourceSets.test.output,
            libraries.junit
    testImplementation(project(':grpc-interop-testing')) {
        // Avoid grpc-netty-shaded dependency
        exclude group: 'io.grpc', module: 'grpc-alts'
        exclude group: 'io.grpc', module: 'grpc-xds'
    }

    undertowTestCompile 'io.undertow:undertow-servlet:2.2.10.Final'

    tomcatTestCompile 'org.apache.tomcat.embed:tomcat-embed-core:9.0.53'

    jetty9TestCompile "org.eclipse.jetty:jetty-servlet:${jetty9Version}",
            "org.eclipse.jetty.http2:http2-server:${jetty9Version}",
            'javax.servlet:javax.servlet-api:4.0.1'// jetty9 only supports servlet 3, force servlet4

    jetty10TestCompile "org.eclipse.jetty:jetty-servlet:${jetty10Version}",
            "org.eclipse.jetty.http2:http2-server:${jetty10Version}"

    jetty11TestCompile "org.eclipse.jetty:jetty-servlet:${jetty11Version}",
            "org.eclipse.jetty.http2:http2-server:${jetty11Version}"
}

javadoc {
    exclude 'io/grpc/servlet/jetty/*'
}

// Set up individual classpaths for each test, to avoid any mismatch,
// and ensure they are only used when supported by the current jvm
check.dependsOn(tasks.register('undertowTest', Test) {
    classpath = sourceSets.undertowTest.runtimeClasspath
    testClassesDirs = sourceSets.undertowTest.output.classesDirs
})
check.dependsOn(tasks.register('tomcatTest', Test) {
    classpath = sourceSets.tomcatTest.runtimeClasspath
    testClassesDirs = sourceSets.tomcatTest.output.classesDirs

    // Provide a temporary directory for tomcat to be deleted after test finishes
    def tomcatTempDir = "$buildDir/tomcat_catalina_base"
    systemProperty 'catalina.base', tomcatTempDir
    doLast {
        file(tomcatTempDir).deleteDir()
    }
})
check.dependsOn(tasks.register('jetty9Test', Test) {
    classpath = sourceSets.jetty9Test.runtimeClasspath
    testClassesDirs = sourceSets.jetty9Test.output.classesDirs
})

// Only run these tests if java 11+ is being used
if (JavaVersion.current().isJava11Compatible()) {
    check.dependsOn(tasks.register('jetty10Test', Test) {
        classpath = sourceSets.jetty10Test.runtimeClasspath
        testClassesDirs = sourceSets.jetty10Test.output.classesDirs
    })
    //check.dependsOn(tasks.register('jetty11Test', Test) {
    //    classpath = sourceSets.jetty11Test.runtimeClasspath
    //    testClassesDirs = sourceSets.jetty11Test.output.classesDirs
    //})
}