plugins {
    id "java-library"
    id "maven-publish"
}

description = "gRPC: Servlet"

// javax.servlet-api 4.0 requires a minimum of Java 8, so we might as well use that source level
sourceCompatibility = 1.8
targetCompatibility = 1.8

def jetty9Version = '9.4.44.v20210927'
def jetty10Version = '10.0.7'

configurations {
    undertowTestImplementation.extendsFrom(testImplementation)
    tomcatTestImplementation.extendsFrom(testImplementation)
    jettyTestImplementation.extendsFrom(testImplementation)
    jetty10TestImplementation.extendsFrom(testImplementation)
}

sourceSets {
    // Disable plain test, we only want to run tests per runtime
    test {
        java {
            exclude '**/*.java'
        }
    }
    // Create a test sourceset for each classpath - could be simplified if we made new test directories
    undertowTest {}
    tomcatTest {}

    jettyTest {
    }

    // Only compile these tests if java 11+ is being used
    if (JavaVersion.current().isJava11Compatible()) {
        jetty10Test {
            java {
                srcDir 'src/jetty/java'
            }
        }
    }
}

dependencies {
    api project(':grpc-core')
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1',
            libraries.javax_annotation // java 9, 10 needs it

    // Jetty9 workaround, we won't include this as a transitive dependency or otherwise
    // use at runtime, unless it is already present
    compileOnly "org.eclipse.jetty.http2:http2-server:${jetty9Version}"

    implementation libraries.guava

    testImplementation project(':grpc-stub'),
            project(':grpc-protobuf'),
            project(':grpc-servlet'),
            project(':grpc-netty'),
            project(':grpc-testing'),
            project(':grpc-auth'),
            project(':grpc-core').sourceSets.test.output,
            project(':grpc-netty').sourceSets.test.output,
            libraries.junit
    testImplementation(project(':grpc-interop-testing')) {
        // Avoid grpc-netty-shaded dependency
        exclude group: 'io.grpc', module: 'grpc-alts'
        exclude group: 'io.grpc', module: 'grpc-xds'
    }

    undertowTestImplementation 'io.undertow:undertow-servlet:2.2.12.Final'

    tomcatTestImplementation 'org.apache.tomcat.embed:tomcat-embed-core:9.0.54'

    jettyTestImplementation "org.eclipse.jetty:jetty-servlet:${jetty9Version}",
            "org.eclipse.jetty.http2:http2-server:${jetty9Version}",
            'javax.servlet:javax.servlet-api:4.0.1'// jetty9 only supports servlet 3, force servlet4

    jetty10TestImplementation "org.eclipse.jetty:jetty-servlet:${jetty10Version}",
            "org.eclipse.jetty.http2:http2-server:${jetty10Version}"
}

javadoc {
    exclude 'io/grpc/servlet/jetty/*'
}

// Set up individual classpaths for each test, to avoid any mismatch,
// and ensure they are only used when supported by the current jvm
check.dependsOn(tasks.register('undertowTest', Test) {
    classpath = sourceSets.undertowTest.runtimeClasspath
    testClassesDirs = sourceSets.undertowTest.output.classesDirs
})
check.dependsOn(tasks.register('tomcat9Test', Test) {
    classpath = sourceSets.tomcatTest.runtimeClasspath
    testClassesDirs = sourceSets.tomcatTest.output.classesDirs

    // Provide a temporary directory for tomcat to be deleted after test finishes
    def tomcatTempDir = "$buildDir/tomcat_catalina_base"
    systemProperty 'catalina.base', tomcatTempDir
    doLast {
        file(tomcatTempDir).deleteDir()
    }
})
check.dependsOn(tasks.register('jetty9Test', Test) {
    classpath = sourceSets.jettyTest.runtimeClasspath
    testClassesDirs = sourceSets.jettyTest.output.classesDirs
})

// Only run these tests if java 11+ is being used
if (JavaVersion.current().isJava11Compatible()) {
    check.dependsOn(tasks.register('jetty10Test', Test) {
        classpath = sourceSets.jetty10Test.runtimeClasspath
        testClassesDirs = sourceSets.jetty10Test.output.classesDirs
    })
}
