plugins {
    id "maven-publish"

    id "com.android.library"
    id "digital.wup.android-maven-publish"
}

description = "gRPC: Cronet Android"

repositories {
    google()
    jcenter()
}

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug { minifyEnabled false }
        release {
            minifyEnabled false
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    testOptions { unitTests { includeAndroidResources = true } }
    lintOptions { disable 'InvalidPackage' }
}

dependencies {
    implementation project(':grpc-core'),
            libraries.cronet_api,
            libraries.errorprone
    implementation (libraries.guava) {
        // prefer our own version from grpc-api instead of Guava's dependency
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        // prefer our own version from libraries instead of Guava's dependency
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        // prefer our own version from libraries instead of Guava's dependency
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }
    runtimeOnly libraries.animalsniffer_annotations
    testImplementation project(':grpc-testing'),
            libraries.cronet_embedded,
            libraries.junit,
            libraries.mockito,
            libraries.robolectric
}

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += files(android.getBootClasspath())
    classpath += files({
        android.libraryVariants.collect { variant ->
            variant.javaCompileProvider.get().classpath
        }
    })
    options {
        // Disable JavaDoc doclint on Java 8.
        if (JavaVersion.current().isJava8Compatible()) {
            addStringOption('Xdoclint:none', '-quiet')
        }
    }
    exclude 'io/grpc/cronet/Internal*'
}

task javadocJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        maven {
            from components.android

            artifact javadocJar
            artifact sourcesJar
        }
    }
}
