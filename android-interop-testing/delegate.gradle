buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
        classpath 'com.android.tools.build:gradle:1.1.0'
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.6.1"
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

repositories {
    mavenCentral()
    mavenLocal()
}

def properties = new Properties()
properties.load(new FileInputStream(file('../version.properties')))

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "io.grpc.android.integrationtest"
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage', 'HardcodedText'
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-1'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${properties.version}"
        }
    }
    generateProtoTasks {
        all()*.builtins {
            javanano {
                // Options added to --javanano_out
                option 'ignore_services=true'
            }
        }
        ofNonTest()*.plugins {
            grpc {
                // Options added to --grpc_out
                option 'nano=true'
            }
        }
    }
}


dependencies {
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.google.android.gms:play-services-base:7.3.0'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    testCompile 'junit:junit:4.12'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile "io.grpc:grpc-android:${properties.version}"
    compile "io.grpc:grpc-protobuf-nano:${properties.version}"
    compile "io.grpc:grpc-stub:${properties.version}"
    compile "io.grpc:grpc-testing:${properties.version}"
}