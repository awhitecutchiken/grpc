plugins {
    id "com.android.application"
    id "com.google.protobuf"
}

description = 'gRPC: Android Integration Testing'

repositories {
    google()
    jcenter()
}

android {
    sourceSets {
        main {
            java {
                srcDirs += "${projectDir}/../interop-testing/src/main/java/"
                setIncludes(["io/grpc/android/integrationtest/**",
                             "io/grpc/testing/integration/AbstractInteropTest.java",
                             "io/grpc/testing/integration/TestServiceImpl.java",
                             "io/grpc/testing/integration/Util.java"])
            }
            proto {
                srcDirs += "${projectDir}/../interop-testing/src/main/proto/"
            }
        }
    }
    compileSdkVersion 26

    defaultConfig {
        applicationId "io.grpc.android.integrationtest"
        // API level 14+ is required for TLS since Google Play Services v10.2
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        debug { minifyEnabled false }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions { disable 'InvalidPackage', 'HardcodedText' }
}

dependencies {
    implementation 'android.arch.lifecycle:common:1.0.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:support-compat:26.1.0'
    implementation 'com.android.support:support-core-ui:26.1.0'
    implementation 'com.android.support:support-core-utils:26.1.0'
    implementation 'com.android.support:support-fragment:26.1.0'
    implementation 'com.google.android.gms:play-services-base:15.0.1'
    implementation 'com.google.android.gms:play-services-basement:15.0.1'
    implementation 'org.hamcrest:hamcrest-core:1.3'

    implementation project(':grpc-api'),
            project(':grpc-auth'),
            project(':grpc-census'),
            project(':grpc-context'),
            project(':grpc-core'),
            project(':grpc-okhttp'),
            project(':grpc-protobuf-lite'),
            project(':grpc-stub'),
            project(':grpc-testing'),
            libraries.google_auth_credentials,
            libraries.guava_failureaccess,
            libraries.j2objc_annotations,
            libraries.jsr305,
            libraries.junit,
            libraries.protobuf_lite,
            libraries.truth

    implementation (libraries.google_auth_oauth2_http) {
        exclude group: 'org.apache.httpcomponents'
    }
    censusApiDependency 'implementation'
    censusGrpcMetricDependency 'implementation'
    guavaDependency 'implementation'

    compileOnly libraries.javax_annotation

    androidTestImplementation 'androidx.test:rules:1.1.0-alpha1'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha1'
}

// Checkstyle doesn't run automatically with android
task checkStyleMain(type: Checkstyle) {
    source 'src/main/java'
    include '**/*.java'
    classpath = files()
}

task checkStyleTest(type: Checkstyle) {
    source 'src/androidTest/java'
    include '**/*.java'
    classpath = files()
}

project.tasks['check'].dependsOn checkStyleMain, checkStyleTest

import net.ltgt.gradle.errorprone.CheckSeverity

tasks.withType(JavaCompile) {
    options.compilerArgs += [
            "-Xlint:-cast"
    ]
    // Reuses source code from grpc-interop-testing, which targets Java 7 (no method references)
    options.errorprone.check("UnnecessaryAnonymousClass", CheckSeverity.OFF)
}

configureProtoCompilation()
