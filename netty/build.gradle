plugins {
    id "java-library"
    id "maven-publish"

    id "me.champeau.gradle.japicmp"
    id "me.champeau.gradle.jmh"
    id "ru.vyarus.animalsniffer"
}

description = "gRPC: Netty"

configurations {
    alpnagent
}

evaluationDependsOn(project(':grpc-core').path)

dependencies {
    alpnagent libraries.jetty_alpn_agent

    api project(':grpc-core'),
            libraries.netty
    implementation libraries.errorprone,
            libraries.netty_proxy_handler
    implementation (libraries.guava) {
        // prefer our own version from grpc-api instead of Guava's dependency
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        // prefer our own version from libraries instead of Guava's dependency
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        // prefer our own version from libraries instead of Guava's dependency
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }
    implementation (libraries.perfmark) {
        // prefer our own versions from libraries instead of PerfMark's dependency
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
    }
    runtimeOnly libraries.animalsniffer_annotations
    // Tests depend on base class defined by core module.
    testImplementation project(':grpc-core').sourceSets.test.output,
            project(':grpc-testing'),
            project(':grpc-testing-proto')
    testRuntimeOnly libraries.conscrypt,
            libraries.netty_epoll,
            libraries.netty_tcnative

    signature "org.codehaus.mojo.signature:java17:1.0@signature"
}

import net.ltgt.gradle.errorprone.CheckSeverity

[compileJava, compileTestJava].each() {
    // Netty retuns a lot of futures that we mostly don't care about.
    it.options.errorprone.check("FutureReturnValueIgnored", CheckSeverity.OFF)
}

javadoc {
    options.links 'http://netty.io/4.1/api/'
    exclude 'io/grpc/netty/Internal*'
}

project.sourceSets {
    main { java { srcDir "${projectDir}/third_party/netty/java" } }
}

test {
    // Allow testing Jetty ALPN in TlsTest
    jvmArgs "-javaagent:" + configurations.alpnagent.asPath
}

jmh {
    // Workaround
    // https://github.com/melix/jmh-gradle-plugin/issues/97#issuecomment-316664026
    includeTests = true
}

checkstyleMain {
    source = source.minus(fileTree(dir: "src/main", include: "**/Http2ControlFrameLimitEncoder.java"))
}
