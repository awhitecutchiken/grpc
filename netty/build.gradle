plugins {
    id "java-library"
    id "maven-publish"

    id "me.champeau.gradle.japicmp"
    id "me.champeau.gradle.jmh"
    id "ru.vyarus.animalsniffer"
}

description = "gRPC: Netty"

configurations {
    alpnagent
}

evaluationDependsOn(project(':grpc-core').path)

dependencies {
    api project(':grpc-api'),
            project(':grpc-core'),
            libraries.jsr305,
            libraries.netty,
            libraries.netty_handler
    implementation libraries.netty_buffer,
            libraries.netty_codec,
            libraries.netty_codec_http,
            libraries.netty_common,
            libraries.netty_transport,
            libraries.netty_proxy_handler
    guavaDependency 'implementation'
    perfmarkDependency 'implementation'
    compileOnly libraries.guava_failureaccess,
            libraries.j2objc_annotations
    // Tests depend on base class defined by core module.
    testImplementation project(':grpc-core').sourceSets.test.output,
            project(':grpc-testing'),
            project(':grpc-testing-proto')
    testRuntimeOnly libraries.netty_tcnative,
            libraries.conscrypt,
            libraries.netty_epoll
    signature "org.codehaus.mojo.signature:java17:1.0@signature"
    alpnagent libraries.jetty_alpn_agent
}

import net.ltgt.gradle.errorprone.CheckSeverity

[compileJava, compileTestJava].each() {
    // Netty retuns a lot of futures that we mostly don't care about.
    it.options.errorprone.check("FutureReturnValueIgnored", CheckSeverity.OFF)
}

javadoc {
    options.links 'http://netty.io/4.1/api/'
    exclude 'io/grpc/netty/Internal*'
}

project.sourceSets {
    main { java { srcDir "${projectDir}/third_party/netty/java" } }
}

test {
    // Allow testing Jetty ALPN in TlsTest
    jvmArgs "-javaagent:" + configurations.alpnagent.asPath
}

jmh {
    // Workaround
    // https://github.com/melix/jmh-gradle-plugin/issues/97#issuecomment-316664026
    includeTests = true
}

checkstyleMain {
    source = source.minus(fileTree(dir: "src/main", include: "**/Http2ControlFrameLimitEncoder.java"))
}
