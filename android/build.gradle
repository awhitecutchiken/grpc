plugins {
    id "maven-publish"

    id "com.android.library"
    id "digital.wup.android-maven-publish"
}

description = 'gRPC: Android'

android {
    compileSdkVersion 28
    defaultConfig {
        consumerProguardFiles "proguard-rules.txt"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions { abortOnError false }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation project(':grpc-core'),
            libraries.errorprone
    implementation (libraries.guava) {
        // prefer our own version from grpc-api instead of Guava's dependency
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        // prefer our own version from libraries instead of Guava's dependency
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        // prefer our own version from libraries instead of Guava's dependency
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }
    runtimeOnly libraries.animalsniffer_annotations
    testImplementation project('::grpc-okhttp'),
            libraries.androidx_test,
            libraries.junit,
            libraries.robolectric,
            libraries.truth
}

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += files(android.getBootClasspath())
    classpath += files({
        android.libraryVariants.collect { variant ->
            variant.javaCompileProvider.get().classpath
        }
    })
    options {
        // Disable JavaDoc doclint on Java 8.
        if (JavaVersion.current().isJava8Compatible()) {
            addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        maven {
            from components.android

            artifact javadocJar
            artifact sourcesJar
        }
    }
}
